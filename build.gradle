//file:noinspection GroovyAssignabilityCheck
//file:noinspection GroovyAccessibility
plugins {
    id 'java'
	id 'application'
	id 'maven-publish'
	id 'org.cadixdev.licenser' version '0.6.1'
	id 'eclipse'
    id "com.github.gmazzo.buildconfig" version "5.2.0"
}

def legacyVersion = "1.7.7"
group 'net.legacyfabric'
version '1.5.3'

version = legacyVersion + "+fabricmc." + project.version

def ENV = System.getenv()
version = version + (ENV.GITHUB_ACTIONS ? "" : "+local")

sourceCompatibility = 1.8
targetCompatibility = 1.8

archivesBaseName = "fabric-meta"

repositories {
    mavenCentral()
}

dependencies {
	implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.9'
	implementation group: 'io.javalin', name: 'javalin', version: '3.13.11'
	implementation group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.32'
	implementation group: 'commons-io', name: 'commons-io', version: '2.8.0'
}

buildConfig {
	className("BuildConstants")
	packageName("net.legacyfabric.meta")

	useJavaOutput()

	buildConfigField(String.class, "VERSION", "$project.version")
}

jar {
	manifest {
		attributes "Main-Class": "net.fabricmc.meta.FabricMeta"
	}
	from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
	duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

mainClassName = 'net.fabricmc.meta.FabricMeta'

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"

	if (JavaVersion.current().isJava9Compatible()) {
		it.options.release = 8
	}
}

license {
	matching(excludes: ['**/BuildConstants.java'], includes: ['**/legacyfabric/**']) {
		header = file("LF-ONLY-HEADER")
	}
	matching(includes: ['**/VersionDatabase.java', '**/MinecraftLauncherMeta.java','**/EndpointsV2.java',
						'**/ProfileHandler.java', '**/MavenUrlVersion.java', '**/ServerBootstrap.java'
	]) {
		header = file('LF-HEADER')
	}
	header file('HEADER')
	include '**/*.java'
	exclude '**/BuildConstants.java'
}

publishing {
	publications {
		maven(MavenPublication) {
			groupId project.group
			artifactId project.archivesBaseName
			version project.version

			artifact jar
		}
	}
	repositories {
		if (ENV.MAVEN_PUBLISH_CREDENTIALS) {
			maven {
				url "https://repo.legacyfabric.net/repository/legacyfabric"
				credentials {
					username ENV.MAVEN_PUBLISH_CREDENTIALS.split(":")[0]
					password ENV.MAVEN_PUBLISH_CREDENTIALS.split(":")[1]
				}
				authentication {
					basic(BasicAuthentication)
				}
			}
		}
	}
}

// A task to ensure that the version being released has not already been released.
task checkVersion {
	doFirst {
		def xml = new URL("https://maven.legacyfabric.net/net/fabricmc/fabric-meta/maven-metadata.xml").text
		def metadata = new XmlSlurper().parseText(xml)
		def versions = metadata.versioning.versions.version*.text();
		if (versions.contains(version)) {
			throw new RuntimeException("${version} has already been released!")
		}
	}
}

publish.mustRunAfter checkVersion
